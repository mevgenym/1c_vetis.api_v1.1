
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.КомандаОтбор.Пометка = Истина;
	
	//Элементы.СписокВетисlink.Видимость = Ложь;
	
	МаксимальноеКоличество = 100;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Элементы.ГруппаОтбор1.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		Элементы.ГруппаОтбор2.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	#КонецЕсли
	
	УстановитьВидимостьОтВерсии();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтаФорма.ИмяФормы Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисНастройки" Тогда
		УстановитьВидимостьОтВерсии();
	ИначеЕсли ИмяСобытия = "ВетисВерсия" Тогда
		УстановитьВидимостьОтВерсии();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаНайти(Команда)
	
	Перем _Отказ, _Ошибка;
	
	Если ВариантПоиска = "Guid" Тогда
		КомандаНайтиПоGuidСервер(_Отказ, _Ошибка);
	ИначеЕсли ВариантПоиска = "GLN" Тогда
		КомандаНайтиПоGLNСервер();
	Иначе
		КомандаНайтиСервер();
	КонецЕсли;
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаЗаполнитьИзСервиса(Команда)
	
	ДобавитьИзСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьИзСервиса(Команда)
	
	ДобавитьИзСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтбор(Команда)
	
	Элементы.КомандаОтбор.Пометка = НЕ Элементы.КомандаОтбор.Пометка;
	
	Элементы.ГруппаОтбор.Видимость = Элементы.КомандаОтбор.Пометка;
	
КонецПроцедуры



&НаКлиенте
Процедура ВариантПоискаПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользованиеПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	
	Выполнить(Элемент.Имя+"Использование=ЗначениеЗаполнено("+Элемент.Имя+");");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтранаПриИзменении(Элемент)
	
	ОтборСтранаИспользование = ЗначениеЗаполнено(ОтборСтрана);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРегионПриИзменении(Элемент)
	
	ОтборРегионИспользование = ЗначениеЗаполнено(ОтборРегион);
	
	ОтборРайонИспользование = ОтборРайонИспользование И ОтборРегионИспользование;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРайонПриИзменении(Элемент)
	
	ОтборРайонИспользование = ЗначениеЗаполнено(ОтборРайон);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВетисВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Элементы.СправочникСписок.ТекущаяСтрока = ВетисВызовСервера.Соответствие_ПолучитьСсылку(Элементы.СписокВетис.ТекущиеДанные.guid, "Справочник.ВетисBusinessEntity");
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Справочник.ВетисBusinessEntity.ФормаОбъекта", Новый Структура("Ключ", Элементы.СправочникСписок.ТекущаяСтрока));
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьВидимостьОтВерсии()
	
	Элементы.ВариантПоиска.СписокВыбора.Очистить();
	Элементы.ВариантПоиска.СписокВыбора.Добавить("NameAdr", "Имя, ИНН, Адрес");
	Элементы.ВариантПоиска.СписокВыбора.Добавить("Guid", "guid");
	
	ВариантПоиска = "NameAdr";
	
	Если ВетисВызовСервера.Версия_2_0() Тогда
		ОтборПолноеИмяИспользование = Ложь;
		Элементы.ГруппаОтборПолноеИмя.Видимость = Ложь;
		Элементы.ГруппаОтборИмя.Видимость = Истина;
		Элементы.ГруппаОтборРегион.Видимость = Истина;
		Элементы.ГруппаОтборРайон.Видимость = Истина;
		Элементы.ВариантПоиска.СписокВыбора.Добавить("GLN", "GLN");
	Иначе
		ОтборИмяИспользование = Ложь;
		ОтборРегионИспользование = Ложь;
		ОтборРайонИспользование = Ложь;
		Элементы.ГруппаОтборПолноеИмя.Видимость = Истина;
		Элементы.ГруппаОтборИмя.Видимость = Ложь;
		Элементы.ГруппаОтборРегион.Видимость = Ложь;
		Элементы.ГруппаОтборРайон.Видимость = Ложь;
		_ВариантПоиска = Элементы.ВариантПоиска.СписокВыбора.НайтиПоЗначению("GLN");
		Если НЕ _ВариантПоиска = Неопределено Тогда
			Элементы.ВариантПоиска.СписокВыбора.Удалить(_ВариантПоиска);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	
	Элементы.ГруппаОтборGuid.Видимость = ВариантПоиска = "Guid";
	Элементы.ГруппаОтборGLN.Видимость = ВариантПоиска = "GLN";
	Элементы.ГруппаОтбор1.Видимость = ВариантПоиска = "NameAdr";
	Элементы.ГруппаОтбор2.Видимость = ВариантПоиска = "NameAdr";
	Элементы.МаксимальноеКоличество.Видимость = ВариантПоиска = "NameAdr";
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()
	
	Элементы.СправочникСписок.Обновить();
	
КонецПроцедуры


&НаСервере
Процедура КомандаНайтиСервер()
	
	Перем _businessEntity, _параметры;
	
	_Версия20 = Ветис.Версия_2_0();
	_ВетисEnterpriseService = ?(_Версия20, ВетисEnterpriseService_2_0, ВетисEnterpriseService);
	_ВетисCerberusEnterpriseСлой1с = ?(_Версия20, ВетисDictionaryСлой1с, ВетисCerberusEnterpriseСлой1с);
	_ВетисIkarСлой1с = ?(_Версия20, ВетисDictionaryСлой1с, ВетисIkarСлой1с);
	
	СписокВетис.Очистить();
	
	_Отбор = Новый Структура;
	
	Если ОтборИмяИспользование Тогда
		_Отбор.Вставить("name", ОтборИмя);
	КонецЕсли;
	
	Если ОтборПолноеИмяИспользование Тогда
		_Отбор.Вставить("fullName", ОтборПолноеИмя);
	КонецЕсли;
	
	Если ОтборИННИспользование Тогда
		_Отбор.Вставить("inn", ОтборИНН);
	КонецЕсли;
	
	Если ОтборФИОИспользование Тогда
		_Отбор.Вставить("fio", ОтборФИО);
	КонецЕсли;
	
	Если ОтборСтранаИспользование ИЛИ ОтборРайонИспользование ИЛИ ОтборРегионИспользование Тогда
		_отборадрес = Новый Структура();
		Если ОтборСтранаИспользование Тогда
			_отборадрес.Вставить("country", Новый Структура("guid", _ВетисIkarСлой1с.Country(ОтборСтрана)));
		КонецЕсли;
		Если ОтборРегионИспользование Тогда
			_отборадрес.Вставить("region", Новый Структура("guid", _ВетисIkarСлой1с.Region(ОтборРегион)));
		КонецЕсли;
		Если ОтборРайонИспользование Тогда
			_отборадрес.Вставить("district", Новый Структура("guid", _ВетисIkarСлой1с.District(ОтборРайон)));
		КонецЕсли;
		_отбор.Вставить("juridicalAddress", _отборадрес);
	КонецЕсли;
	
	_ОтборТип = _ВетисCerberusEnterpriseСлой1с.BusinessEntityType(ОтборТип);
	_ОтборАдрес = ВРег(ОтборАдрес);
	
	_параметры = Новый Структура("filter,count", _отбор, ?(МаксимальноеКоличество > 1000, Неопределено, МаксимальноеКоличество));
	
	Пока _ВетисEnterpriseService.GetBusinessEntityListNext(_businessEntity, _параметры) Цикл
		
		Если ОтборАдресИспользование И Найти(ВРег(_businessEntity.juridicalAddress.addressView), _ОтборАдрес) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОтборТипИспользование И НЕ _ОтборТип = _businessEntity.type Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВетисДобавить(_businessEntity);
		
		Если МаксимальноеКоличество > 0 И СписокВетис.Количество() >= МаксимальноеКоличество Тогда Прервать; КонецЕсли;
		
	КонецЦикла;
	
	СписокВетис.Сортировать("name");
	
КонецПроцедуры

&НаСервере
Процедура КомандаНайтиПоGuidСервер(пОтказ = Ложь, пОшибка = "")
	
	СписокВетис.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ОтборGuid) Тогда Возврат; КонецЕсли;
	
	_businessEntity = ?(Ветис.Версия_2_0(), ВетисEnterpriseService_2_0, ВетисEnterpriseService).GetBusinessEntityByGuid(ОтборGuid, пОтказ, пОшибка);
	
	Если НЕ пОтказ = Истина Тогда
		СписокВетисДобавить(_businessEntity);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КомандаНайтиПоGLNСервер()
	
	СписокВетис.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ОтборGLN) Тогда Возврат; КонецЕсли;
	
	Если НЕ Ветис.Версия_2_0() Тогда Возврат; КонецЕсли;
	
	_businessMember = ВетисEnterpriseService_2_0.GetBusinessMemberByGLN(ОтборGLN);
	
	_businessEntity = _businessMember.businessEntity;
	
	СписокВетисДобавить(_businessMember.businessEntity);
	
КонецПроцедуры

&НаСервере
Процедура СписокВетисДобавить(пBusinessEntity)
	
	_СписокВетисСтрока = СписокВетис.Добавить();
	
	ЗаполнитьЗначенияСвойств(_СписокВетисСтрока, пBusinessEntity);
	
	Если пBusinessEntity.type = ВетисКонстанты.BusinessEntityType_JURIDICAL() Тогда
		_СписокВетисСтрока.name = ?(пBusinessEntity.name = Неопределено, пBusinessEntity.fullName, пBusinessEntity.name);
		_СписокВетисСтрока.fullName = ?(пBusinessEntity.fullName = Неопределено, пBusinessEntity.name, пBusinessEntity.fullName);
	Иначе
		_СписокВетисСтрока.name = пBusinessEntity.fio;
	КонецЕсли;
	
	_СписокВетисСтрока.address = пBusinessEntity.juridicalAddress.addressView;
	
	_СписокВетисСтрока.link = Ветис.Соответствие_ПолучитьСсылку(пBusinessEntity.guid, "Справочник.ВетисBusinessEntity");
	
КонецПроцедуры


&НаСервере
Процедура ДобавитьИзСервиса()
	
	Для каждого _ТекущиеДанныеИд Из Элементы.СписокВетис.ВыделенныеСтроки Цикл
		_ТекущиеДанные = СписокВетис.НайтиПоИдентификатору(_ТекущиеДанныеИд);
		ВетисDictionaryСлой1с.BusinessEntity(_ТекущиеДанные.guid);
	КонецЦикла;
	
КонецПроцедуры
