
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ФормаКомандаОтбор.Пометка = Истина;
	
	СправочникСписок.Параметры.УстановитьЗначениеПараметра("BusinessEntity", Объект.ХозяйствующийСубъект);
	
	МаксимальноеКоличество = 100;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Элементы.ГруппаОтборИмя.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		Элементы.ГруппаОтборАдрес.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	#КонецЕсли
	
	СправочникСписок.Параметры.УстановитьЗначениеПараметра("BusinessEntity", Объект.ХозяйствующийСубъект);
	
	УстановитьВидимостьОтВерсии();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтаФорма.ИмяФормы Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисНастройки" Тогда
		УстановитьВидимостьОтВерсии();
	ИначеЕсли ИмяСобытия = "ВетисВерсия" Тогда
		УстановитьВидимостьОтВерсии();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаНайти(Команда)
	
	Перем _Отказ, _Ошибка;
	
	Если НЕ ЗначениеЗаполнено(ВариантПоиска) Тогда
		Возврат
	КонецЕсли;
	
	_Вариант = ВариантПоиска;
	
	Если _Вариант = "BE" Тогда
		КомандаНайтиПоGuidСервер(Объект.ХозяйствующийСубъект, Истина, _Отказ, _Ошибка);
	ИначеЕсли _Вариант = "GuidBE" Тогда
		КомандаНайтиПоGuidСервер(ОтборBusinessEntityGuid, Истина, _Отказ, _Ошибка);
	ИначеЕсли _Вариант = "GuidEnt" Тогда
		КомандаНайтиПоGuidСервер(ОтборGuid, , _Отказ, _Ошибка);
	ИначеЕсли ВариантПоиска = "GLN" Тогда
		КомандаНайтиПоGLNСервер(_Отказ, _Ошибка);
	Иначе
		КомандаНайтиПоИмениНаСервере(_Отказ, _Ошибка);
	КонецЕсли;
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьИзСервиса(Команда)
	
	ДобавитьИзСервиса();
	
	ОбновитьСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьИзСервиса(Команда)
	
	ДобавитьИзСервиса();
	
	ОбновитьСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтвязатьПредприятиеХС(Команда)
	
	_ТекущиеДанные = Элементы.СправочникСписок.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено ИЛИ НЕ ЗначениеЗаполнено(_ТекущиеДанные.guid) Тогда
		Возврат;
	КонецЕсли;
	
	КомандаОтвязатьПредприятиеХСНаСервере(Объект.ХозяйствующийСубъект, _ТекущиеДанные.Ссылка);
	
	ОбновитьСписок();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КомандаОтвязатьПредприятиеХСНаСервере(BusinessEntity, Enterprise)
	
	РегистрыСведений.ВетисBusinessMembers.Удалить(BusinessEntity, Enterprise);
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаПривязатьПредприятиеХС(Команда)
	
	_ТекущиеДанные = Элементы.СправочникСписок.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено ИЛИ НЕ ЗначениеЗаполнено(_ТекущиеДанные.guid) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.ВетисBusinessMembers.ФормаЗаписи", ПолучитьПараметры(Объект.ХозяйствующийСубъект, _ТекущиеДанные.Ссылка));
	
	ОбновитьСписок();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметры(BusinessEntity, Enterprise)
	
	_Запись = РегистрыСведений.ВетисBusinessMembers.СоздатьМенеджерЗаписи();
	_Запись.BusinessEntity = BusinessEntity;
	_Запись.Enterprise = Enterprise;
	_Запись.Прочитать();
	
	Если _Запись.Выбран() Тогда
		Возврат Новый Структура("Ключ", РегистрыСведений.ВетисBusinessMembers.СоздатьКлючЗаписи(Новый Структура("BusinessEntity, Enterprise", BusinessEntity, Enterprise)));
	Иначе
		Возврат Новый Структура("BusinessEntity, Enterprise", BusinessEntity, Enterprise);
	КонецЕсли;
	
КонецФункции


&НаКлиенте
Процедура КомандаОтбор(Команда)
	
	Элементы.ФормаКомандаОтбор.Пометка = НЕ Элементы.ФормаКомандаОтбор.Пометка;
	
	Элементы.ГруппаОтбор.Видимость = Элементы.ФормаКомандаОтбор.Пометка;
	
КонецПроцедуры


&НаКлиенте
Процедура ВариантПоискаПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользованиеПриИзменении(Элемент)
	
	//УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	
	Выполнить(Элемент.Имя+"Использование=ЗначениеЗаполнено("+Элемент.Имя+");");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХозяйствующийСубъектПриИзменении(Элемент)
	
	СправочникСписок.Параметры.УстановитьЗначениеПараметра("BusinessEntity", Объект.ХозяйствующийСубъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтранаПриИзменении(Элемент)
	
	ОтборСтранаИспользование = ЗначениеЗаполнено(ОтборСтрана);
	
	ОтборРегионИспользование = ОтборРегионИспользование И ЗначениеЗаполнено(ОтборРегион);
	
	ОтборРайонИспользование = ОтборРайонИспользование И ЗначениеЗаполнено(ОтборРайон);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРегионПриИзменении(Элемент)
	
	ОтборРегионИспользование = ЗначениеЗаполнено(ОтборРегион);
	
	ОтборРайонИспользование = ОтборРайонИспользование И ЗначениеЗаполнено(ОтборРайон);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРайонПриИзменении(Элемент)
	
	ОтборРайонИспользование = ЗначениеЗаполнено(ОтборРайон);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВетисВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Элементы.СправочникСписок.ТекущаяСтрока = ВетисВызовСервера.Соответствие_ПолучитьСсылку(Элементы.СписокВетис.ТекущиеДанные.guid, "Справочник.ВетисEnterprise");
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Справочник.ВетисEnterprise.ФормаОбъекта", Новый Структура("Ключ", Элементы.СправочникСписок.ТекущаяСтрока));
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьВидимостьОтВерсии()
	
	_Версия20 = ВетисВызовСервера.Версия_2_0() ИЛИ ВетисВызовСервера.Версия_2_1();
	
	Элементы.ВариантПоиска.СписокВыбора.Очистить();
	Элементы.ВариантПоиска.СписокВыбора.Добавить("NameAdr", "Имя, Адрес");
	Элементы.ВариантПоиска.СписокВыбора.Добавить("GuidEnt", "guid Предприятия");
	Элементы.ВариантПоиска.СписокВыбора.Добавить("BE", "ХС");
	Элементы.ВариантПоиска.СписокВыбора.Добавить("GuidBE", "guid ХС");
	
	Если _Версия20 Тогда
		Элементы.ВариантПоиска.СписокВыбора.Добавить("GLN", "GLN");
	КонецЕсли;
	
	ВариантПоиска = "NameAdr";
	
	Элементы.ГруппаОтборНомер.Видимость = _Версия20;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	
	//ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СправочникСписок.Отбор, "ХозяйствующийСубъект", Объект.ХозяйствующийСубъект, , ВариантПоиска = "BE");
	
	Элементы.ОтборХозяйствующийСубъект.Видимость = ВариантПоиска = "BE";
	Элементы.ОтборBusinessEntityGuid.Видимость = ВариантПоиска = "GuidBE";
	Элементы.ОтборGuid.Видимость = ВариантПоиска = "GuidEnt";
	Элементы.ГруппаОтборGLN.Видимость = ВариантПоиска = "GLN";
	Элементы.ГруппаОтборИмя.Видимость = ВариантПоиска = "NameAdr";
	Элементы.ГруппаОтборАдрес.Видимость = ВариантПоиска = "NameAdr";
	Элементы.МаксимальноеКоличество.Видимость = ВариантПоиска = "NameAdr";
	
	Элементы.СписокВетисКомандаПривязатьПредприятиеХС.Видимость = ВариантПоиска = "BE";
	Элементы.СписокВетисКомандаОтвязатьПредприятиеХС.Видимость = ВариантПоиска = "BE";
	
	//Элементы.ОтборНаименование.Доступность = ОтборНаименованиеИспользование;
	//Элементы.ОтборРайон.Доступность = ОтборРайонИспользование;
	//Элементы.ОтборРегион.Доступность = ОтборРегионИспользование;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()
	
	Элементы.СправочникСписок.Обновить();
	
КонецПроцедуры


&НаСервере
Процедура КомандаНайтиПоИмениНаСервере(пОтказ = Ложь, пОшибка = "")
	
	Перем _Enterprise, _Параметры;
	
	_Версия20 = Ветис.Версия_2_0() ИЛИ Ветис.Версия_2_1();
	
	СписокВетис.Очистить();
	
	ВсегоКоличество = 0;
	
	_отбор = ВетисDictionary.Enterprise();
	
	Если ОтборНаименованиеИспользование Тогда
		_отбор.name = ОтборНаименование;
	КонецЕсли;
	
	Если ОтборРайонИспользование ИЛИ ОтборРегионИспользование Тогда
		_отбор.address = ВетисDictionary.Address();
		Если ОтборСтранаИспользование Тогда
			_отбор.address.country = ВетисDictionary.Country(ВетисDictionaryСлой1с.Country(ОтборСтрана));
		КонецЕсли;
		Если ОтборРегионИспользование Тогда
			_отбор.address.region = ВетисDictionary.Region(ВетисDictionaryСлой1с.Region(ОтборРегион));
		КонецЕсли;
		Если ОтборРайонИспользование Тогда
			_отбор.address.district = ВетисDictionary.District(ВетисDictionaryСлой1с.District(ОтборРайон));
		КонецЕсли;
	КонецЕсли;
	
	Если ОтборНомерИспользование И _Версия20 Тогда
		_отбор.numberList = ВетисDictionary.EnterpriseNumberList();
		_отбор.numberList.enterpriseNumber.Добавить(ОтборНомер);
	КонецЕсли;
	
	_параметры = Новый Структура("filter", _отбор);
	
	Пока ВетисEnterpriseService.GetRussianEnterpriseListNext(_Enterprise, _Параметры, пОтказ, пОшибка) Цикл
		СписокВетисДобавить(_Enterprise);
		Если МаксимальноеКоличество > 0 И СписокВетис.Количество() >= МаксимальноеКоличество Тогда Прервать; КонецЕсли;
	КонецЦикла;
	
	ВсегоКоличество = _Параметры.ListResponse.total;
	
КонецПроцедуры

&НаСервере
Процедура КомандаНайтиПоGuidСервер(пGuid, пОтборПоХС = Ложь, пОтказ = Ложь, пОшибка = "")
	
	Перем _Enterprise, _Параметры;
	
	СписокВетис.Очистить();
	
	ВсегоКоличество = 0;
	
	Если НЕ ЗначениеЗаполнено(пGuid) Тогда Возврат; КонецЕсли;
	
	Если пОтборПоХС Тогда
		
		Если ТипЗнч(пGuid) = Тип("Строка") Тогда
			_Guid = пGuid;
		Иначе
			_Guid = ВетисDictionaryСлой1с.BusinessEntity(пGuid);
		КонецЕсли;
		
		Пока ВетисEnterpriseService.GetActivityLocationListNext(_Guid, _Enterprise, _Параметры, пОтказ, пОшибка) Цикл
			СписокВетисДобавить(_Enterprise);
		КонецЦикла;
		
		ВсегоКоличество = _Параметры.ListResponse.total;
	Иначе
		
		_Enterprise = ВетисEnterpriseService.GetEnterpriseByGuid(пGuid, пОтказ, пОшибка);
		
		ВетисОбщегоНазначения.Сериализовать(_Enterprise, ВетисПовтИсп.ФабрикаОбщая());
		Если НЕ пОтказ = Истина Тогда
			СписокВетисДобавить(_Enterprise);
			ВсегоКоличество = 1;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КомандаНайтиПоGLNСервер(пОтказ = Ложь, пОшибка = "")
	
	СписокВетис.Очистить();
	
	ВсегоКоличество = 0;
	
	Если НЕ ЗначениеЗаполнено(ОтборGuid) Тогда Возврат; КонецЕсли;
	
	_businessMember = ВетисEnterpriseService.GetBusinessMemberByGLN(ОтборGLN, пОтказ, пОшибка);
	
	Если НЕ пОтказ = Истина Тогда
		СписокВетисДобавить(_businessMember.enterprise);
		ВсегоКоличество = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИзСервиса()
	
	Для каждого _ТекущиеДанныеИд Из Элементы.СписокВетис.ВыделенныеСтроки Цикл
		
		_ТекущиеДанные = СписокВетис.НайтиПоИдентификатору(_ТекущиеДанныеИд);
		
		_Ссылка = ВетисDictionaryСлой1с.Enterprise(_ТекущиеДанные.guid);
		
		РегистрыСведений.ВетисBusinessMembers.Добавить(Объект.ХозяйствующийСубъект, _Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Процедура СписокВетисДобавить(пEnterprise)
	
	Если НЕ (пEnterprise.last И пEnterprise.active) Тогда
		Возврат;
	КонецЕсли;
	
	//в 2.0 в тесте найдены дубли 2017-10-26
	Если СписокВетис.НайтиСтроки(Новый Структура("guid", пEnterprise.guid)).Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	_СписокВетисСтрока = СписокВетис.Добавить();
	
	_СписокВетисСтрока.last = пEnterprise.last;
	_СписокВетисСтрока.active = пEnterprise.active;
	
	_СписокВетисСтрока.guid = пEnterprise.guid;
	_СписокВетисСтрока.name = пEnterprise.name;
	_СписокВетисСтрока.type = пEnterprise.type;
	_СписокВетисСтрока.owner = ?(пEnterprise.owner = Неопределено, "", пEnterprise.owner.guid);
	
	Если НЕ пEnterprise.address = Неопределено Тогда
		_СписокВетисСтрока.country  = ?(пEnterprise.address.country = Неопределено, "", пEnterprise.address.country.guid);
		_СписокВетисСтрока.region   = ?(пEnterprise.address.region = Неопределено, "", пEnterprise.address.region.guid);
		_СписокВетисСтрока.district = ?(пEnterprise.address.district = Неопределено, "", пEnterprise.address.district.guid);
		_СписокВетисСтрока.locality = ?(пEnterprise.address.locality = Неопределено, "", пEnterprise.address.locality.guid);
		_СписокВетисСтрока.street   = ?(пEnterprise.address.street = Неопределено, "", пEnterprise.address.street.guid);
		_СписокВетисСтрока.address  = пEnterprise.address.addressView;
	КонецЕсли;
	
	Если НЕ пEnterprise.numberList = Неопределено Тогда
		Для каждого _enterpriseNumber Из пEnterprise.numberList.enterpriseNumber Цикл
			_СписокВетисСтрока.numberList.Добавить(_enterpriseNumber);
		КонецЦикла;
	КонецЕсли;
	
	_СписокВетисСтрока.link = НЕ Ветис.Соответствие_ПолучитьСсылку(пEnterprise.guid, "Справочник.ВетисEnterprise", Ложь) = Ложь;
	
КонецПроцедуры
