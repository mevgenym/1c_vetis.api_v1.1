
Функция Добавить(пСсылка, пОперация = Неопределено, пНастройка = Неопределено, пОтветственный = Неопределено, пПараметры = Неопределено) Экспорт
	
	_Период = ТекущаяДатаСеанса();
	_Миллисекунды = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	_УникальныйИдентификатор = Новый УникальныйИдентификатор;
	
	Если пОперация = Неопределено Тогда
		_Операция = ПредопределенноеЗначение("Перечисление.ВетисОтложенныеОперации.ПоУмолчанию");
	ИначеЕсли ТипЗнч(пОперация) = Тип("Строка") Тогда
		_Операция = ПредопределенноеЗначение("Перечисление.ВетисОтложенныеОперации."+пОперация);
	Иначе
		_Операция = пОперация;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ВетисОтложенныеОперации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(_УникальныйИдентификатор);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Период = _Период;
	Запись.Миллисекунды = _Миллисекунды % 1000;
	Запись.УникальныйИдентификатор = _УникальныйИдентификатор;
	Запись.Объект = пСсылка;
	Запись.Операция = _Операция;
	Запись.Настройка = пНастройка;
	Запись.Ответственный = ?(пОтветственный = Неопределено, Ветис.ТекущийПользователь(), пОтветственный);
	Запись.Параметры = Новый ХранилищеЗначения(пПараметры);
	
	НаборЗаписей.Записать();
	
	Возврат _УникальныйИдентификатор;
	
КонецФункции

Процедура Удалить(пУникальныйИдентификатор, пСообщение = Неопределено, пОшибка = Ложь, пЗавершено = Истина) Экспорт
	
	НаборЗаписей = РегистрыСведений.ВетисОтложенныеОперации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(пУникальныйИдентификатор);
	НаборЗаписей.Прочитать();
	
	Для каждого Запись Из НаборЗаписей Цикл
		Запись.Завершено = пЗавершено;
		Запись.ЗавершеноДата = ТекущаяДатаСеанса();
		Запись.Ошибка = пОшибка;
		Запись.ПредставлениеСообщения = пСообщение;
		Запись.Сообщение = Новый ХранилищеЗначения(пСообщение);
		//Запись.Результат = Новый ХранилищеЗначения(пРезультат);
	КонецЦикла;
	
	Если НаборЗаписей.Модифицированность() Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура Скопировать(пУникальныйИдентификатор) Экспорт
	
	НаборЗаписей = РегистрыСведений.ВетисОтложенныеОперации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(пУникальныйИдентификатор);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запись = НаборЗаписей[0];
	
	Добавить(Запись.Объект, Запись.Операция, Запись.Настройка, Запись.Ответственный, Запись.Параметры);
	
КонецПроцедуры

Процедура Блокировка(пУникальныйИдентификатор, пЗначение = Истина) Экспорт
	
	НаборЗаписей = РегистрыСведений.ВетисОтложенныеОперации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(пУникальныйИдентификатор);
	НаборЗаписей.Прочитать();
	
	Для каждого Запись Из НаборЗаписей Цикл
		Если НЕ Запись.Блокировка = пЗначение Тогда
			Запись.Блокировка = пЗначение;
		КонецЕсли;
	КонецЦикла;
	
	Если НаборЗаписей.Модифицированность() Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры
