
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Enterprise") Тогда
		
		ВетисОбщегоНазначения.ДобавитьОтбор(СписокПартии.Отбор, "Enterprise", Параметры.Enterprise);
		
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	Если Элементы.СписокПартии.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	_ТекущиеДанные = Элементы.СписокПартии.ТекущиеДанные;
	
	ЗначениеВыбора = Новый Структура;
	ЗначениеВыбора.Вставить("Guid",            _ТекущиеДанные.Guid);
	ЗначениеВыбора.Вставить("ProductType",     _ТекущиеДанные.ProductType);
	ЗначениеВыбора.Вставить("Product",         _ТекущиеДанные.Product);
	ЗначениеВыбора.Вставить("SubProduct",      _ТекущиеДанные.SubProduct);
	ЗначениеВыбора.Вставить("ProductItem",     _ТекущиеДанные.ProductItem);
	ЗначениеВыбора.Вставить("ProductItemName", ?(ПустаяСтрока(_ТекущиеДанные.ProductItemName), _ТекущиеДанные.ProductItem, _ТекущиеДанные.ProductItemName));
	//ЗначениеВыбора.Вставить("dateOfProductionFirst", _ТекущиеДанные.dateOfProductionFirst);
	//ЗначениеВыбора.Вставить("dateOfProductionSecond", _ТекущиеДанные.dateOfProductionSecond);
	ЗначениеВыбора.Вставить("Unit",            _ТекущиеДанные.Unit);
	ЗначениеВыбора.Вставить("producer",        _ТекущиеДанные.producer);
	ЗначениеВыбора.Вставить("Количество",      _ТекущиеДанные.Volume);
	ЗначениеВыбора.Вставить("Упаковка",        ПолучитьУпаковку(_ТекущиеДанные.Уид));
	
	Если ЗначениеЗаполнено(_ТекущиеДанные.dateOfProductionInformal) Тогда
		ЗначениеВыбора.Вставить("dateOfProductionInformal", _ТекущиеДанные.dateOfProductionInformal);
	Иначе
		ЗначениеВыбора.Вставить("dateOfProductionInformal", формат(_ТекущиеДанные.dateOfProductionFirst, "ДЛФ=D") + " - " + формат(_ТекущиеДанные.dateOfProductionSecond, "ДЛФ=D"));
	КонецЕсли;
	
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьУпаковку(пУид) 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	_Упаковка.level КАК Уровень,
	|	_Упаковка.packingType КАК Упаковка,
	|	_Упаковка.packingAmount КАК Количество,
	|	_Маркировка.value КАК Маркировка,
	|	_Маркировка.class КАК КлассМаркировки
	|ИЗ
	|	РегистрСведений.ВетисPackageList КАК _Упаковка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисProductMarks КАК _Маркировка
	|		ПО _Упаковка.уид = _Маркировка.уид
	|			И _Упаковка.level = _Маркировка.level
	|			И _Упаковка.packingType = _Маркировка.packingType
	|ГДЕ
	|	_Упаковка.уид = &уид
	|ИТОГИ
	|	МАКСИМУМ(Количество)
	|ПО
	|	Уровень,
	|	Упаковка");
	
	Запрос.УстановитьПараметр("уид", пУид);
	
	Результат = Запрос.Выполнить();
	ВыборкаУровень = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	_список = Новый Массив;
	Пока ВыборкаУровень.Следующий() Цикл
		
		ВыборкаТип = ВыборкаУровень.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаТип.Следующий() Цикл
			
			_Маркировка = Новый Массив;
			
			ВыборкаМарк = ВыборкаТип.Выбрать();
			Пока ВыборкаМарк.Следующий() Цикл
				Если НЕ ВыборкаМарк.Маркировка = NULL Тогда
					_Маркировка.Добавить(Новый Структура("Маркировка,КлассМаркировки",ВыборкаМарк.Маркировка,ВыборкаМарк.КлассМаркировки));
				КонецЕсли;
			КонецЦикла;
			
			_строка = Новый Структура();
			_строка.Вставить("Уровень",ВыборкаТип.Уровень);
			_строка.Вставить("Упаковка",ВыборкаТип.Упаковка);
			_строка.Вставить("Количество",ВыборкаТип.Количество);
			_строка.Вставить("Маркировка", _Маркировка);
			
			_список.Добавить(_строка);
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат _список;
	
КонецФункции


&НаКлиенте
Процедура СписокПартииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КомандаВыбрать(Неопределено);
	
КонецПроцедуры
