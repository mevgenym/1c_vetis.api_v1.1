
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Ссылка = Параметры.Ссылка;
	
	//Количество = Параметры.Количество;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	_Таблица.Наименование,
	|	_Таблица.volume КАК Количество,
	|	_Таблица.unit КАК unit,
	|	_Таблица.PackageList.(
	|		Ссылка,
	|		НомерСтроки,
	|		КлючСтроки,
	|		level,
	|		packingType,
	|		quantity
	|	),
	|	_Таблица.ProductMarks.(
	|		Ссылка,
	|		НомерСтроки,
	|		КлючСвязи,
	|		value,
	|		class
	|	)
	|ИЗ
	|	Справочник.ВетисStockEntry КАК _Таблица
	|ГДЕ
	|	_Таблица.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", Параметры.Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Заголовок = Выборка.Наименование + " ("+Параметры.Количество+")";
	
	Количество = ?(Параметры.Количество = 0, Выборка.Количество, Параметры.Количество);
	
	unit = Выборка.unit;
	
	Доступно = Выборка.Количество;
	
	ВыборкаТЧ = Выборка.PackageList.Выбрать();
	Пока ВыборкаТЧ.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(PackageList.Добавить(), ВыборкаТЧ);
	КонецЦикла;
	
	ВыборкаТЧ = Выборка.ProductMarks.Выбрать();
	Пока ВыборкаТЧ.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ProductMarks.Добавить(), ВыборкаТЧ);
	КонецЦикла;
	
	Если Параметры.Свойство("Упаковка") Тогда
		Если ТипЗнч(Параметры.Упаковка) = Тип("Массив")
			И Параметры.Упаковка.Количество() > 0 Тогда
			Для каждого _PackageListLine Из PackageList Цикл
				Для каждого _УпаковкаСтрока Из Параметры.Упаковка Цикл
					Если _PackageListLine.packingType = _УпаковкаСтрока.Упаковка
						И _PackageListLine.level = _УпаковкаСтрока.Уровень Тогда
						_PackageListLine.Использовать = Истина;
						_PackageListLine.Количество = _УпаковкаСтрока.Количество;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		ИначеЕсли ТипЗнч(Параметры.Упаковка) = Тип("Структура") Тогда
			Для каждого _PackageListLine Из PackageList Цикл
				_УпаковкаСтрока = Параметры.Упаковка;
				Если _PackageListLine.packingType = _УпаковкаСтрока.Упаковка
					И _PackageListLine.level = _УпаковкаСтрока.Уровень Тогда
					_PackageListLine.Использовать = Истина;
					_PackageListLine.Количество = _УпаковкаСтрока.Количество;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		//по умолчанию сразу помечаем первую
		Если PackageList.Количество() > 0 Тогда
			PackageList[0].Использовать = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаГотово(Команда)
	
	_Упаковка = Новый Структура("Уровень,Упаковка,Количество");
	_Маркировка = Новый Массив;
	Для каждого _УпаковкаСтрока Из PackageList Цикл
		Если _УпаковкаСтрока.Использовать И _УпаковкаСтрока.Количество > 0 Тогда
			_Упаковка.Вставить("Уровень", _УпаковкаСтрока.level);
			_Упаковка.Вставить("Упаковка", _УпаковкаСтрока.packingType);
			//_Упаковка.Вставить("Количество", _УпаковкаСтрока.quantity);
			_Упаковка.Вставить("Количество", _УпаковкаСтрока.Количество);//?(_УпаковкаСтрока.Количество = 0, _УпаковкаСтрока.quantity, _УпаковкаСтрока.Количество);
			Для каждого _МаркировкаСтрока Из ProductMarks Цикл
				Если _МаркировкаСтрока.КлючСвязи = _УпаковкаСтрока.КлючСтроки Тогда
					_Маркировка.Добавить(Новый Структура("Значение,Класс", _МаркировкаСтрока.value, _МаркировкаСтрока.class));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Новый Структура("Ссылка,unit,Количество,Упаковка,Маркировка", Ссылка, unit, Количество, _Упаковка, _Маркировка));
	
КонецПроцедуры

&НаКлиенте
Процедура PackageListПриАктивизацииСтроки(Элемент)
	
	_ТекущиеДанные = Элементы.PackageList.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ProductMarks.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", _ТекущиеДанные.КлючСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура PackageListПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КлючСвязи = КлючСвязи;
		Элемент.ТекущиеДанные.КлючСтроки = ПолучитьКлючСтрокиУпаковка();
		Элементы.ProductMarks.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", Элемент.ТекущиеДанные.КлючСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура PackageListПередУдалением(Элемент, Отказ)
	
	Для каждого _Строка Из ProductMarks.НайтиСтроки(Новый Структура("КлючСвязи", Элемент.ТекущиеДанные.КлючСтроки)) Цикл
		ProductMarks.Удалить(_Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючСтрокиУпаковка()
	
	_список = PackageList.Выгрузить().ВыгрузитьКолонку("КлючСтроки");
	
	Для _Индекс = 1 По 999 Цикл
		Если _список.Найти(_Индекс) = Неопределено Тогда
			Возврат _Индекс;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура PackageListИспользоватьПриИзменении(Элемент)
	
	УстановитьТекущаяСтрока();
	
КонецПроцедуры

&НаКлиенте
Процедура PackageListquantityПриИзменении(Элемент)
	
	УстановитьТекущаяСтрока();
	
КонецПроцедуры

&НаКлиенте
Процедура PackageListpackingTypeПриИзменении(Элемент)
	
	УстановитьТекущаяСтрока();
	
КонецПроцедуры

&НаКлиенте
Процедура PackageListlevelПриИзменении(Элемент)
	
	УстановитьТекущаяСтрока();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущаяСтрока()
	
	_ТекущаяСтрока = Элементы.PackageList.ТекущиеДанные;
	
	_ТекущаяСтрока.Использовать = Истина;
	
	Для каждого _Строка Из PackageList Цикл
		Если НЕ _Строка = _ТекущаяСтрока Тогда
			_Строка.Использовать = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
