
#Область НастройкиПодключения

Функция ПолучитьНастройкиПодключения(пСсылка = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	_Таблица.Логин,
	|	_Таблица.Пароль,
	|	_Таблица.КлючАПИ,
	|//1	_Таблица.BusinessEntity.guid КАК issuerId
	|//3	_Таблица.BusinessEntity.guid КАК issuerId
	|//2	&issuerId КАК issuerId
	|ИЗ
	|	Справочник.ВетисНастройкиПодключения КАК _Таблица
	|ГДЕ
	|	ИСТИНА
	|//1	И _Таблица.Ссылка = &Ссылка
	|//2	И _Таблица.ИдентификаторыХС.issuerID = &issuerId
	|//3	И _Таблица.ИдентификаторыХС.BusinessEntity = &BusinessEntity
	|	И ИСТИНА");
	
	Запрос.УстановитьПараметр("Ссылка", пСсылка);
	
	Если ТипЗнч(пСсылка) = Тип("СправочникСсылка.ВетисНастройкиПодключения") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//1", "");
	ИначеЕсли ТипЗнч(пСсылка) = Тип("Строка") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//2", "");
		Запрос.УстановитьПараметр("issuerId", пСсылка);
	ИначеЕсли ТипЗнч(пСсылка) = Тип("СправочникСсылка.ВетисBusinessEntity") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//3", "");
		Запрос.УстановитьПараметр("BusinessEntity", пСсылка);
	ИначеЕсли ЗначениеЗаполнено(ТекущийПользователь_Настройка()) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//1", "");
		Запрос.УстановитьПараметр("Ссылка", ТекущийПользователь_Настройка());
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//1", "");
		Запрос.УстановитьПараметр("Ссылка", Настройки_ОсновнаяНастройка());
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		_Настройки = Новый Структура;
		_Настройки.Вставить("КлючАПИ",  Выборка.КлючАПИ);
		_Настройки.Вставить("Логин",    Выборка.Логин);
		_Настройки.Вставить("Пароль",   Выборка.Пароль);
		_Настройки.Вставить("issuerId",   Выборка.issuerId);
		
	КонецЕсли;
	
	Возврат _Настройки;
	
КонецФункции

Функция Настройки_ОсновнаяНастройка(пЗначение = Неопределено) Экспорт
	
	Если пЗначение = Неопределено Тогда
		Возврат Константы.ВетисОсновнаяНастройка.Получить();
	Иначе
		Константы.ВетисОсновнаяНастройка.Установить(пЗначение);
	КонецЕсли;
	
КонецФункции

Функция Настройки_Тестовый() Экспорт
	
	Возврат Настройки_ОсновнаяНастройка().Тестовый;
	
КонецФункции

Функция Настройки_ЦелевойСервис() Экспорт
	
	Возврат Настройки_ОсновнаяНастройка().Сервис;
	
КонецФункции

Функция Настройки_ОсновнойХС() Экспорт
	
	Возврат Настройки_ОсновнаяНастройка().BusinessEntity;
	
КонецФункции

Функция ТекущийПользователь() Экспорт
	
	Возврат Ветис.ТекущийПользователь();
	
	//_ТекущийПользователь = Ветис.ТекущийПользователь();
	//
	//Запрос = Новый Запрос(
	//"ВЫБРАТЬ
	////|	_Таблица.Ссылка КАК Пользователь,
	//|	_Таблица.User КАК ПользовательВетис,
	//|	_Таблица.User.post КАК Должность,
	//|	_Таблица.User.login КАК Логин,
	//|	_Таблица.User.Наименование КАК Имя
	//|ИЗ
	//|	Справочник.ВетисПользователи КАК _Таблица
	//|ГДЕ
	//|	_Таблица.Ссылка = &Ссылка");
	//
	//Запрос.УстановитьПараметр("Ссылка", _ТекущийПользователь);
	//
	//Результат = Запрос.Выполнить();
	//Выборка = Результат.Выбрать();
	//
	//_Структура = Новый Структура("Пользователь,ПользовательВетис, Логин, Имя, Должность", _ТекущийПользователь);
	//
	//Если Выборка.Следующий() Тогда
	//	ЗаполнитьЗначенияСвойств(_Структура, Выборка);
	//КонецЕсли;
	//
	//Возврат _Структура;
	
КонецФункции

Функция ТекущийПользователь_Настройка() Экспорт
	
	Возврат Ветис.ТекущийПользователь().Настройка;
	
КонецФункции

Функция ТекущийПользователь_Имя() Экспорт
	
	Возврат Ветис.ТекущийПользователь().Наименование;
	
КонецФункции

Функция ТекущийПользователь_Логин() Экспорт
	
	//Возврат Ветис.ТекущийПользователь().User.login;
	Возврат Ветис.ТекущийПользователь().Логин;
	
КонецФункции

Функция ТекущийПользователь_Должность() Экспорт
	
	//Возврат Ветис.ТекущийПользователь().User.post;
	Возврат Ветис.ТекущийПользователь().Должность;
	
КонецФункции

Функция Версия() Экспорт
	
	Возврат Настройки_ОсновнаяНастройка().ВерсияAPI;
	
КонецФункции

#КонецОбласти

#Область Фабрика

Функция ФабрикаОбщая() Экспорт
	
	_url = XSDПолучитьURL();
	
	Если _url.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	_фабрика = СоздатьФабрикуXDTO(_url);
	
	_Пакеты = Новый Массив;
	
	Для Каждого _Пакет Из _фабрика.Пакеты Цикл
		_Пакеты.Добавить(_Пакет);
	КонецЦикла;
	
	Для Каждого _Пакет Из ВетисSoapEnvelope.Фабрика().Пакеты Цикл
		_Пакеты.Добавить(_Пакет);
	КонецЦикла;
	
	_фабрика = Новый ФабрикаXDTO(, _Пакеты);
	
	Возврат _фабрика;
	
КонецФункции

Функция ФабрикаWS(пИмяСервиса) Экспорт
	
	_ФабрикаXDTO = ФабрикаОбщая();
	
	_Определения = Определения(пИмяСервиса);
	
	_URI = Новый Массив;
	_URI.Добавить(ВетисRegistry_2_0.ServiceURI());
	_URI.Добавить(ВетисBase.ServiceURI());
	
	_ФабрикаWS = Новый ФабрикаXDTO(_Определения.ФабрикаXDTO.ЭкспортМоделиXDTO(_URI), _ФабрикаXDTO.Пакеты);
	
	_Пакеты = Новый Массив;
	_Пакеты.Добавить(_ФабрикаXDTO.Пакеты.Получить(ВетисSoapEnvelope.URI()));
	
	Для Каждого _Пакет Из _ФабрикаWS.Пакеты Цикл
		_Пакеты.Добавить(_Пакет);
	КонецЦикла;
	
	_фабрика = Новый ФабрикаXDTO(, _Пакеты);
	
	Возврат _фабрика;
	
КонецФункции

Функция ФабрикаMA(пИмяСервиса, пURI) Экспорт
	
	_ФабрикаXDTO = ФабрикаОбщая();
	
	_Определения = Определения(пИмяСервиса);
	
	_URI = Новый Массив;  
	
	// создаем врем. фабрику на основе пакетов из конфиги и web сервиса
	// нам нужны 2 пакета из ws, но пакет с application нужен локальный
	_URI.Добавить(ВетисApplication.ServiceURI());
	_URI.Добавить(ВетисBase.ServiceURI());
	
	_ФабрикаWS = Новый ФабрикаXDTO(_Определения.ФабрикаXDTO.ЭкспортМоделиXDTO(_URI), _ФабрикаXDTO.Пакеты);
	
	_Пакеты = Новый Массив;
	_Пакеты.Добавить(_ФабрикаXDTO.Пакеты.Получить(пURI));
	_Пакеты.Добавить(_ФабрикаXDTO.Пакеты.Получить(ВетисSoapEnvelope.URI()));
	
	Для Каждого _Пакет Из _ФабрикаWS.Пакеты Цикл
		_Пакеты.Добавить(_Пакет);
	КонецЦикла;
	
	_фабрика = Новый ФабрикаXDTO(, _Пакеты);
	
	Возврат _фабрика;
	
КонецФункции

#КонецОбласти

#Область Прокси

Функция Определения(пИмяСервиса) Экспорт
	
	Возврат ОпределенияПоURL(WSDLПолучить(пИмяСервиса).URL);
	
КонецФункции

Функция ОпределенияПоURL(пURL) Экспорт
	
	_ЗащищенноеСоедиенение = Новый ЗащищенноеСоединениеOpenSSL;
	
	_ТаймАутПрокси = 60;
	
	Попытка
		Если _ЗащищенноеСоедиенение = Неопределено Тогда
			_Определения = Новый WSОпределения(пURL,,,, _ТаймАутПрокси);
		Иначе
			_Определения = Новый WSОпределения(пURL,,,, _ТаймАутПрокси, _ЗащищенноеСоедиенение);
		КонецЕсли;
	Исключение
		ВетисОбщегоНазначения.ВывестиСообщениеОбОшибке(ИнформацияОбОшибке());
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат _Определения;
	
КонецФункции

Функция Прокси(пИмяСервиса) Экспорт
	
	_ЗащищенноеСоедиенение = Новый ЗащищенноеСоединениеOpenSSL;
	
	Попытка
		
		_wsdl = WSDLПолучить(пИмяСервиса);
		
		_Определения = ОпределенияПоURL(_wsdl.URL);
		
		Если _ЗащищенноеСоедиенение = Неопределено Тогда
			_Прокси = Новый WSПрокси(_Определения, _wsdl.URI, _wsdl.Сервис, _wsdl.Порт,, Ветис.Настройки_ВремяОжиданияОтвета());
		Иначе
			_Прокси = Новый WSПрокси(_Определения, _wsdl.URI, _wsdl.Сервис, _wsdl.Порт,, Ветис.Настройки_ВремяОжиданияОтвета(), _ЗащищенноеСоедиенение);
		КонецЕсли;
		
		_Настройки = ПолучитьНастройкиПодключения();
		
		_Прокси.Пользователь = _Настройки.Логин;
		_Прокси.Пароль = _Настройки.Пароль;
		
		Возврат _Прокси;
		
	Исключение
		ВетисОбщегоНазначения.ВывестиСообщениеОбОшибке(ИнформацияОбОшибке());
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область WSDL

Функция ПолучитьТаблицуWSDL(пТестовый = Неопределено, пСервис = Неопределено, пВерсия = Неопределено)
	
	_таблица = Неопределено;
	
	_Тестовый = ?(пТестовый = Неопределено, Настройки_Тестовый(), пТестовый);
	
	_Сервис = ?(пСервис = Неопределено, Настройки_ЦелевойСервис(), пСервис);
	
	Если Ложь Тогда
		
	ИначеЕсли Ветис.Версия_2_1(пВерсия) Тогда
		
		_КорневойКаталог = "http://api.vetrf.ru/schema/platform/services/2.1-RC-last/";
		_суф = ?(_Тестовый, "_v2.1_pilot.wsdl", "_v2.1_production.wsdl");
		_имя = ?(_Сервис = Перечисления.ВетисЦелевойСервис.mercury_vu_service, "ams-mercury-vu.service", "ams-mercury-g2b.service");
		
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.amsMercuryService, "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _КорневойКаталог+_имя+_суф, Истина);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.ApplicationManagementService, "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _КорневойКаталог+"ApplicationManagementService_v2.0.wsdl");
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.DictionaryService, "http://api.vetrf.ru/schema/cdm/registry/dictionary/service/v2", "DictionaryServiceBindingQSService", "DictionaryServiceBindingQSPort", _КорневойКаталог+"DictionaryService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.EnterpriseService, "http://api.vetrf.ru/schema/cdm/registry/enterprise/service/v2", "EnterpriseServiceBindingQSService", "EnterpriseServiceBindingQSPort", _КорневойКаталог+"EnterpriseService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.IkarService,       "http://api.vetrf.ru/schema/cdm/registry/ikar/service/v2",       "IkarServiceBindingQSService",       "IkarServiceBindingQSPort",       _КорневойКаталог+"IkarService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.ProductService,    "http://api.vetrf.ru/schema/cdm/registry/product/service/v2",    "ProductServiceBindingQSService",    "ProductServiceBindingQSPort",    _КорневойКаталог+"ProductService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.RegionalizationService, "http://api.vetrf.ru/schema/cdm/registry/r13n/service/v2",  "RegionalizationServiceBindingQSService", "RegionalizationServiceBindingQSPort", _КорневойКаталог+"RegionalizationService"+_суф);
		
	ИначеЕсли Ветис.Версия_2_0(пВерсия) Тогда
		
		_КорневойКаталог = "http://api.vetrf.ru/schema/platform/services/2.0-last/";
		_суф = ?(_Тестовый, "_v2.0_pilot.wsdl", "_v2.0_production.wsdl");
		_имя = ?(_Сервис = Перечисления.ВетисЦелевойСервис.mercury_vu_service, "ams-mercury-vu.service", "ams-mercury-g2b.service");
		
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.amsMercuryService, "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _КорневойКаталог+_имя+_суф, Истина);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.ApplicationManagementService, "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _КорневойКаталог+"ApplicationManagementService_v1.1.wsdl");
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.DictionaryService, "http://api.vetrf.ru/schema/cdm/registry/dictionary/service/v2", "DictionaryServiceBindingQSService", "DictionaryServiceBindingQSPort", _КорневойКаталог+"DictionaryService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.EnterpriseService, "http://api.vetrf.ru/schema/cdm/registry/enterprise/service/v2", "EnterpriseServiceBindingQSService", "EnterpriseServiceBindingQSPort", _КорневойКаталог+"EnterpriseService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.IkarService,       "http://api.vetrf.ru/schema/cdm/registry/ikar/service/v2",       "IkarServiceBindingQSService",       "IkarServiceBindingQSPort",       _КорневойКаталог+"IkarService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.ProductService,    "http://api.vetrf.ru/schema/cdm/registry/product/service/v2",    "ProductServiceBindingQSService",    "ProductServiceBindingQSPort",    _КорневойКаталог+"ProductService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.RegionalizationService, "http://api.vetrf.ru/schema/cdm/registry/r13n/service/v2",  "RegionalizationServiceBindingQSService", "RegionalizationServiceBindingQSPort", _КорневойКаталог+"RegionalizationService"+_суф);
		
	ИначеЕсли Ветис.Версия_1_4(пВерсия) Тогда
		
		_КорневойКаталог = "http://api.vetrf.ru/schema/platform/";
		
		_суф = ?(_Тестовый, "_v1.4_pilot.wsdl", "_v1.3_production.wsdl");
		
		//для совместиовсти с 2.0
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.amsMercuryService, "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _КорневойКаталог+"services/ApplicationManagementService"+_суф, Истина);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.ApplicationManagementService, "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _КорневойКаталог+"services/ApplicationManagementService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.DictionaryService, "http://api.vetrf.ru/schema/cdm/registry/service", "DictionaryServiceBindingQSService", "DictionaryServiceBindingQSPort", _КорневойКаталог+"services/DictionaryService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.EnterpriseService, "http://api.vetrf.ru/schema/cdm/registry/service", "EnterpriseServiceBindingQSService", "EnterpriseServiceBindingQSPort", _КорневойКаталог+"cerberus/services/EnterpriseService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.IkarService,       "http://api.vetrf.ru/schema/cdm/registry/service", "IkarServiceBindingQSService",       "IkarServiceBindingQSPort",       _КорневойКаталог+"ikar/services/IkarService"+_суф);
		WSDLДобавить(_таблица, Перечисления.ВетисСервис.ProductService,    "http://api.vetrf.ru/schema/cdm/registry/service", "ProductServiceBindingQSService",    "ProductServiceBindingQSPort",    _КорневойКаталог+"services/ProductService"+_суф);
		
	Иначе
		
		ВетисОбщегоНазначения.ВывестиСообщение(Ветис.ВерсияТекстОшибки());
		
		ВызватьИсключение Ветис.ВерсияТекстОшибки();
		
	КонецЕсли;
	
	Возврат _таблица;
	
КонецФункции

Процедура WSDLДобавить(пТаблица, пИмя, пURI, пСервис, пПорт, пURL, пОпределятьАдрес = Ложь)
	
	Если пТаблица = Неопределено Тогда
		пТаблица = Новый ТаблицаЗначений;
		//пТаблица.Колонки.Добавить("Версия");
		пТаблица.Колонки.Добавить("Имя");
		пТаблица.Колонки.Добавить("URI");
		пТаблица.Колонки.Добавить("Сервис");
		пТаблица.Колонки.Добавить("Порт");
		пТаблица.Колонки.Добавить("URL");
		пТаблица.Колонки.Добавить("Адрес");
		пТаблица.Колонки.Добавить("Составные");
	КонецЕсли;
	
	_строка = пТаблица.Добавить();
	//_строка.Версия = пВерсия;
	_строка.Имя    = пИмя;
	_строка.URI    = пURI;
	_строка.Сервис = пСервис;
	_строка.Порт   = пПорт;
	_строка.URL    = пURL;
	
	Если пОпределятьАдрес = Истина Тогда
		_определения = ВетисПовтИсп.ОпределенияПоURL(_строка.URL);
		_сервис = ПолучитьЭлементКоллекции(_Определения.Сервисы, _строка.Сервис);
		Если НЕ _сервис = Неопределено Тогда
			_точка = ПолучитьЭлементКоллекции(_сервис.ТочкиПодключения, _строка.Порт);
			Если НЕ _точка = Неопределено Тогда
				_строка.Адрес     = _точка.Местоположение;
				_строка.Составные = РазложитьАдрес(_точка.Местоположение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция WSDLПолучить(пИмяСервиса, пОпределятьАдрес = Ложь) Экспорт
	
	_таблица = ПолучитьТаблицуWSDL();
	
	_строка = _таблица.Найти(пИмяСервиса, "Имя");
	
	Если пОпределятьАдрес = Истина И ПустаяСтрока(_строка.Адрес) Тогда
		_определения = ВетисПовтИсп.ОпределенияПоURL(_строка.URL);
		_сервис = ПолучитьЭлементКоллекции(_Определения.Сервисы, _строка.Сервис);
		Если НЕ _сервис = Неопределено Тогда
			_точка = ПолучитьЭлементКоллекции(_сервис.ТочкиПодключения, _строка.Порт);
			Если НЕ _точка = Неопределено Тогда
				_строка.Адрес     = _точка.Местоположение;
				_строка.Составные = РазложитьАдрес(_точка.Местоположение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат _строка;
	
КонецФункции

Функция WSDLПолучитьURL() Экспорт
	
	_таблица = ПолучитьТаблицуWSDL();
	
	_список = Новый Массив;
	
	Для каждого _строка Из _таблица Цикл
		_список.Добавить(_строка.url);
	КонецЦикла;
	
	Возврат _список;
	
КонецФункции

Функция ПолучитьЭлементКоллекции(пКоллекция, пИмя)
	
	Для каждого _Элемент Из пКоллекция Цикл
		Если _Элемент.Имя = пИмя Тогда
			Возврат _Элемент;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Функция РазложитьАдрес(пСтрока)
	
	_результат = Новый Структура;
	
	_строка = пСтрока;
	
	_позиция = Найти(_строка, "://");
	
	_результат.Вставить("ЗащищенноеСоединение", ?(ВРег(Лев(_строка, _позиция - 1)) = "HTTPS", Новый ЗащищенноеСоединениеOpenSSL, Неопределено));
	
	_строка = Сред(_строка, _позиция + 3);
	
	_позиция = Найти(_строка, "/");
	
	_результат.Вставить("Ресурс", Сред(_строка, _позиция + 1));
	
	_строка = Лев(_строка, _позиция - 1);
	
	_позиция = Найти(_строка, ":");
	
	Если _позиция = 0 Тогда
		_результат.Вставить("Сервер", _строка);
		_результат.Вставить("Порт", Неопределено);
	Иначе
		_результат.Вставить("Сервер", Лев(_строка, _позиция - 1));
		_результат.Вставить("Порт", Число(Сред(_строка, _позиция + 1)));
	КонецЕсли;
	
	Возврат _результат;
	
КонецФункции

#КонецОбласти

#Область XSD

Функция ПолучитьТаблицуXSD(пТестовый = Неопределено, пСервис = Неопределено, пВерсия = Неопределено)
	
	_таблица = Неопределено;
	
	_Тестовый = ?(пТестовый = Неопределено, Настройки_Тестовый(), пТестовый);
	
	_Сервис = ?(пСервис = Неопределено, Настройки_ЦелевойСервис(), пСервис);
	
	Если Ложь Тогда
		
	ИначеЕсли Ветис.Версия_2_1(пВерсия) Тогда
		
		_КорневойКаталог = "http://api.vetrf.ru/schema/platform/services/2.1-RC-last/";
		_суф = "_v2.1.xsd";
		_имя = ?(_Сервис = Перечисления.ВетисЦелевойСервис.mercury_vu_service, "mercury_vu_applications", "mercury_g2b_applications");
		
		XSDДобавить(_таблица, _КорневойКаталог+"application_v2.0.xsd",          ВетисApplication.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"base_v2.0.xsd",                 ВетисBase.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"dictionary"+_суф,               ВетисDictionary.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"document"+_суф,                 ВетисMercuryVetdocument.URI());
		XSDДобавить(_таблица, _КорневойКаталог+_имя+_суф, ВетисMercuryApplications.URI());
		
	ИначеЕсли Ветис.Версия_2_0(пВерсия) Тогда
		
		_КорневойКаталог = "http://api.vetrf.ru/schema/platform/services/2.0-last/";
		_суф = "_v2.0.xsd";
		_имя = ?(_Сервис = Перечисления.ВетисЦелевойСервис.mercury_vu_service, "mercury_vu_applications", "mercury_g2b_applications");
		
		XSDДобавить(_таблица, _КорневойКаталог+"application_v1.1.xsd",          ВетисApplication.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"base_v1.1.xsd",                 ВетисBase.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"dictionary"+_суф,               ВетисDictionary.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"document"+_суф,                 ВетисMercuryVetdocument.URI());
		XSDДобавить(_таблица, _КорневойКаталог+_имя+_суф, ВетисMercuryApplications.URI());
		
	ИначеЕсли Ветис.Версия_1_4(пВерсия) Тогда
		
		_КорневойКаталог = "http://api.vetrf.ru/schema/platform/mercury/g2b/";
		_суф = "_v1.4.xsd";
		
		XSDДобавить(_таблица, _КорневойКаталог+"application"+_суф,         ВетисApplication.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"argus_common"+_суф,        ВетисArgusCommon_1_4.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"production"+_суф,          ВетисArgusProduction_1_4.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"argus_shipment"+_суф,      ВетисArgusShipment_1_4.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"base"+_суф,                ВетисBase.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"enterprise"+_суф,          ВетисCerberusEnterprise_1_4.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"ikar"+_суф,                ВетисIkar_1_4.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"applications"+_суф,        ВетисMercuryApplications.URI());
		XSDДобавить(_таблица, _КорневойКаталог+"veterinary_document"+_суф, ВетисMercuryVetdocument.URI());
		
	Иначе
		
		ВетисОбщегоНазначения.ВывестиСообщение(Ветис.ВерсияТекстОшибки());
		
		ВызватьИсключение Ветис.ВерсияТекстОшибки();
		
	КонецЕсли;
	
	Возврат _таблица;
	
КонецФункции

Процедура XSDДобавить(пТаблица, URL, URI)
	
	Если пТаблица = Неопределено Тогда
		пТаблица = Новый ТаблицаЗначений;
		пТаблица.Колонки.Добавить("URI");
		пТаблица.Колонки.Добавить("URL");
	КонецЕсли;
	
	_строка = пТаблица.Добавить();
	_строка.URI    = URI;
	_строка.URL    = URL;
	
КонецПроцедуры

Функция XSDПолучитьURL() Экспорт
	
	_таблица = ПолучитьТаблицуXSD();
	
	_список = Новый Массив;
	
	Для каждого _строка Из _таблица Цикл
		_список.Добавить(_строка.url);
	КонецЦикла;
	
	Возврат _список;
	
КонецФункции

#КонецОбласти
