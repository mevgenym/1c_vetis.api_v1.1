
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	_Статус = Неопределено;
	Для каждого _СтрокаПартии Из Партии Цикл
		_СтрокаПартии.Статус = _СтрокаПартии.ВСД.Статус;
		Если НЕ ЗначениеЗаполнено(_СтрокаПартии.Статус) Тогда
			//пропуск
		ИначеЕсли _Статус = Неопределено Тогда
			_Статус = _СтрокаПартии.Статус;
		ИначеЕсли НЕ _Статус = _СтрокаПартии.Статус Тогда
			//разный статус всд партий, в шапке ставим пустой статус
			_Статус = Неопределено;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Статус = _Статус;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Товары.Очистить();
	
	Упаковка.Очистить();
	
	Маркировка.Очистить();
	
	Партии.Очистить();
	
	_всд = ?(ВСД.Количество() = 0, ВСД.Добавить(), ВСД[0]);
	
	_ттн = ?(ТТН.Количество() = 0, ТТН.Добавить(), ТТН[0]);
	
	_ттн.Серия = "";
	_ттн.Номер = "";
	_ттн.Дата  = ТекущаяДата();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Отправитель = Ветис.ТекущийХС();
	
	ОтправительПредприятие = Ветис.ТекущееПредприятие();
	
	_всд = ВСД.Добавить();
	
	_всд.НазначениеГруза       = Ветис.Настройки_ВСД_НазначениеГруза();
	_всд.БлагополучиеМестности = Ветис.Настройки_ВСД_БлагополучиеМестности();
	_всд.Ветсанэкспертиза20    = Ветис.Настройки_ВСД_ВСЭ();
	_всд.КонтрольГосВетврачем  = Ветис.Настройки_ВСД_КонтрольГосВетврачем();
	
	_ттн = ТТН.Добавить();
	
	_ттн.Дата      = ТекущаяДата();
	_ттн.Транспорт = Ветис.Настройки_ТТН_Транспорт();
	
	Если ЗначениеЗаполнено(_ттн.Транспорт) Тогда
		_ттн.Тип            = _ттн.Транспорт.ТипТранспорта;
		_ттн.Посредник      = _ттн.Транспорт.Перевозчик;
		_ттн.СпособХранения = _ттн.Транспорт.СпособХранения;
	Иначе
		_ттн.Тип            = Ветис.Настройки_ТТН_Тип();
		_ттн.Посредник      = Ветис.Настройки_ТТН_Посредник();
		_ттн.СпособХранения = Ветис.Настройки_ТТН_СпособХранения();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
КонецПроцедуры
