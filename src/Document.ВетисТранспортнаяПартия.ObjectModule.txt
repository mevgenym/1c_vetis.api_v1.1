
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Количество = 0;
	
	_Статус = Неопределено;
	Для каждого _СтрокаПартии Из Партии Цикл
		_СтрокаПартии.Статус = _СтрокаПартии.ВСД.Статус;
		Если НЕ ЗначениеЗаполнено(_СтрокаПартии.Статус) Тогда
			//пропуск
		ИначеЕсли _Статус = Неопределено Тогда
			_Статус = _СтрокаПартии.Статус;
		ИначеЕсли НЕ _Статус = _СтрокаПартии.Статус Тогда
			//разный статус всд партий, в шапке ставим пустой статус
			_Статус = Неопределено;
			Прервать;
		КонецЕсли;
		Количество = Количество + _СтрокаПартии.Количество;
	КонецЦикла;
	
	КоличествоМест = 0;
	Для каждого _СтрокаУпаковка Из Упаковка Цикл
		КоличествоМест = КоличествоМест + _СтрокаУпаковка.Количество;
	КонецЦикла;
	
	Статус = _Статус;
	
	ВетисХозяйственныеОперации.ПривязатьХСПредприятие(Отправитель, ОтправительПредприятие, Истина);
	
	ВетисХозяйственныеОперации.ПривязатьХСПредприятие(Получатель, ПолучательПредприятие, Истина);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Товары.Очистить();
	
	Упаковка.Очистить();
	
	Маркировка.Очистить();
	
	Партии.Очистить();
	
	_всд = ?(ВСД.Количество() = 0, ВСД.Добавить(), ВСД[0]);
	
	_ттн = ?(ТТН.Количество() = 0, ТТН.Добавить(), ТТН[0]);
	
	_ттн.Серия = "";
	_ттн.Номер = "";
	_ттн.Дата  = ТекущаяДата();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Отправитель = Ветис.ТекущийХС();
	
	ОтправительПредприятие = Ветис.ТекущееПредприятие();
	
	_всд = ВСД.Добавить();
	_всд.НазначениеГруза       = Ветис.Настройки_ВСД_НазначениеГруза();
	_всд.БлагополучиеМестности = Ветис.Настройки_ВСД_БлагополучиеМестности();
	_всд.Ветсанэкспертиза20    = Ветис.Настройки_ВСД_ВСЭ();
	_всд.КонтрольГосВетврачем  = Ветис.Настройки_ВСД_КонтрольГосВетврачем();
	
	_ттн = ТТН.Добавить();
	_ттн.Дата      = ТекущаяДата();
	_ттн.Тип       = Перечисления.ВетисDocumentType.ТовароТранспортнаяНакладная;
	_ттн.Транспорт = Ветис.Настройки_ТТН_Транспорт();
	
	Если ЗначениеЗаполнено(_ттн.Транспорт) Тогда
		_ттн.ТипТранспорта  = _ттн.Транспорт.ТипТранспорта;
		_ттн.НомерТранспорта= _ттн.Транспорт.Номер;
		_ттн.НомерПрицепа   = _ттн.Транспорт.НомерПрицепа;
		_ттн.Посредник      = _ттн.Транспорт.Перевозчик;
		_ттн.СпособХранения = _ттн.Транспорт.СпособХранения;
	Иначе
		_ттн.ТипТранспорта  = Перечисления.ВетисTransportType.Автомобильный;
		_ттн.Посредник      = Ветис.Настройки_ТТН_Посредник();
		_ттн.СпособХранения = Ветис.Настройки_ТТН_СпособХранения();
	КонецЕсли;
	
	Ответственный = Ветис.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
КонецПроцедуры
