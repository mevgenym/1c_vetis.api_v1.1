
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("Дата", НачалоДня(ТекущаяДата()));
	
	Если Параметры.Свойство("Ключ") Тогда
		Элементы.Список.ТекущаяСтрока = Параметры.Ключ;
		Заголовок = "" + Параметры.Ключ;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Количество") Тогда
		ТребуемоеКоличество = Параметры.Количество;
		Заголовок = ?(Заголовок = "", "Требуемое количество: ", Заголовок) + ?(ТребуемоеКоличество > 0, " (" + ТребуемоеКоличество + ")", "");
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("КоличествоМест") Тогда
		ТребуемоеКоличествоМест = Параметры.КоличествоМест;
	КонецЕсли;
	
	Если Параметры.Свойство("ТекущийДокумент") Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ТекущийДокумент", Параметры.ТекущийДокумент);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ТекущийДокумент", Неопределено);
	КонецЕсли;
	
	Если Параметры.Свойство("BusinessEntity") Тогда
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "BusinessEntity", Параметры.BusinessEntity);
	КонецЕсли;
	
	Если Параметры.Свойство("Enterprise") Тогда
		Если ТипЗнч(Параметры.Enterprise) = Тип("Массив") Тогда
			_Список = Новый СписокЗначений;
			_Список.ЗагрузитьЗначения(Параметры.Enterprise);
			ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "Enterprise", _Список, ВидСравненияКомпоновкиДанных.ВСписке);
		Иначе
			ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "Enterprise", Параметры.Enterprise);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("producer") Тогда
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "producer", Параметры.producer);
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = "" + Параметры.Заголовок + ?(ТребуемоеКоличество > 0, " (" + ТребуемоеКоличество + ")", "");
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	ТекущаяДата = ТекущаяДата();
	
	НачалоМесяца = НачалоМесяца(ТекущаяДата);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КомандаВыбрать(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	_ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	_Параметры = Новый Структура("Ссылка, Количество, КоличествоМест", _ТекущиеДанные.Ссылка, ТребуемоеКоличество, ТребуемоеКоличествоМест);
	
	ОткрытьФорму("Справочник.ВетисStockEntry.Форма.ФормаВводаКоличества", _Параметры, ЭтаФорма,,,, Новый ОписаниеОповещения("СписокВыборОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры
