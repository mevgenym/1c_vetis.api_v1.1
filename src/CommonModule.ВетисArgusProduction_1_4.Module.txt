
// Тип, описывающий тип продукции.
Функция ProductTypeОписание(value) Экспорт
	
	Если      value = 1 Тогда
		Возврат "Мясо и мясопродукты";
	ИначеЕсли value = 2 Тогда
		Возврат "Корма и кормовые добавки";
	ИначеЕсли value = 3 Тогда
		Возврат "Живые животные";
	ИначеЕсли value = 4 Тогда
		Возврат "Лекарственные средства";
	ИначеЕсли value = 5 Тогда
		Возврат "Пищевые продукты";
	ИначеЕсли value = 6 Тогда
		Возврат "Непищевые продукты и другое";
	ИначеЕсли value = 7 Тогда
		Возврат "Рыба и морепродукты";
	ИначеЕсли value = 8 Тогда
		Возврат "Продукция, не требующая разрешения";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Тип, описывающий продукцию.
// Параметры:
//  guid - Строка - guid
//       - Структура - структура со значениями свойств
Функция Product(guid = Неопределено) Экспорт
	
	_Объект = Создать("Product");
	
	ВетисОбщегоНазначения.УстановитьЗначение(_Объект, guid);
	
	Возврат _Объект;
	
КонецФункции

//Тип, описывающий вид продукции.
//Параметры:
// guid - Строка - guid
//      - Структура - структура со значениями свойств
Функция SubProduct(guid = Неопределено) Экспорт
	
	_Объект = Создать("SubProduct");
	
	ВетисОбщегоНазначения.УстановитьЗначение(_Объект, guid);
	
	Возврат _Объект;
	
КонецФункции

//Тип, описывающий наименование продукции.
//Параметры:
// guid - Строка - guid
//      - Структура - структура со значениями свойств
Функция ProductItem(guid = Неопределено, name = Неопределено) Экспорт
	
	_Объект = Создать("ProductItem");
	
	ВетисОбщегоНазначения.УстановитьЗначение(_Объект, guid, "guid");
	ВетисОбщегоНазначения.УстановитьЗначение(_Объект, name, "name");
	
	Возврат _Объект;
	
КонецФункции

//Тип, описывающий список наименований продукции
//Параметры:
// productItemList - Наименование продукции в номенклатуре производителя. Массив - dt:ProductItem 0..*
Функция ProductItemList(productItemList = Неопределено) Экспорт
	
	_Объект = Создать("ProductItemList");
	
	ВетисОбщегоНазначения.УстановитьЗначение(_Объект, productItemList, "productItem");
	
	_Объект.count = _Объект.productItem.Количество();
	_Объект.offset = 0;
	_Объект.total = 0;
	
	Возврат _Объект;
	
КонецФункции

//http://vetrf.ru/vetrf-forum/posts/list/645/6855.page#40125
//Либо занесите номенклатуру (методы для этого есть), либо не указывайте его в ВСД. Этот параметр желателен, но не обязателен.

//http://vetrf.ru/vetrf-forum/posts/list/1185/6855.page#46389
//Четвёртый уровень продукции станет обязательным в Ветис.API 2.0 и в Меркурии 6.0 

//Тип, описывающий операцию внесения изменений в реестр наименований продукции
//Параметры:
// type - bs:RegisterModificationType - Тип операции (CREATE, FIND_OR_CREATE, UPDATE)
// enterprise - ent:Enterprise - Предприятие-производитель
// productItemList - Массив
//   На данный момент разрешается добавлять только по одной позиции.
//   Используется при добавлении и редактировании записи.
Функция PSLModificationOperation(type, enterprise, productItemList = Неопределено) Экспорт
	
	_Объект = Создать("PSLModificationOperation");
	
	ВетисОбщегоНазначения.УстановитьЗначение(_Объект, type, "type");
	ВетисОбщегоНазначения.УстановитьЗначение(_Объект, enterprise, "enterprise");
	
	Если type = ВетисКонстанты.RegisterModificationType_DELETE() Тогда
		ВетисОбщегоНазначения.УстановитьЗначение(_Объект, ProductItemList(productItemList), "affectedList");
	Иначе
		ВетисОбщегоНазначения.УстановитьЗначение(_Объект, ProductItemList(productItemList), "resultingList");
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

Функция Создать(пИмя, пФабрика = Неопределено) Экспорт
	
	Возврат ВетисОбщегоНазначения.Создать(пИмя, URI(), пФабрика);
	
КонецФункции

Функция URI() Экспорт
	
	Возврат "http://api.vetrf.ru/schema/cdm/argus/production";
	
КонецФункции
