
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтправительСвязь = Истина;
	
	ПолучательСвязь = Истина;
	
	//МногоуровневаяУпаковка = Ветис.Настройки_МногоуровневаяУпаковка();
	
	ЗаполнитьРеквизитыФормы();
	
	Для каждого _Элемент Из Элементы Цикл
		Если ТипЗнч(_Элемент) = Тип("ПолеФормы") Тогда
			Если Лев(_Элемент.ПутьКДанным, 3) = "всд" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "ВСДПриИзменении");
			ИначеЕсли Лев(_Элемент.ПутьКДанным, 3) = "ттн" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "ТТНПриИзменении");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//Элементы.ГруппаДокументОснование.Видимость = Ветис.Настройки_Интеграция();
	
	//Элементы.ТоварыКомандаЗаполнитьТовары.Видимость = Ветис.Настройки_Интеграция();
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЗаписатьРеквизитыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтаФорма.ИмяФормы Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисНастройки" Тогда
		УстановитьВидимость();
	ИначеЕсли ИмяСобытия = "ВетисВерсия" Тогда
		УстановитьВидимость();
	ИначеЕсли ИмяСобытия = "ВетисОтладка" Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисТестовый" Тогда
		//
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Если ИсточникВыбора.ИмяФормы = "Документ.ВетисТранспортнаяПартия.Форма.ФормаПодбора" Тогда
			Если Объект.Партии.НайтиСтроки(Новый Структура("ЗаписьСЖ",ВыбранноеЗначение.Ссылка)).Количество() = 0 Тогда
				_СтрокаПартии = Объект.Партии.Добавить();
				_СтрокаПартии.ЗаписьСЖ = ВыбранноеЗначение.Ссылка;
				_СтрокаПартии.Unit = ВыбранноеЗначение.Unit;
				_СтрокаПартии.Количество = ВыбранноеЗначение.Количество;
				_СтрокаПартии.КлючСтроки = ПолучитьКлючСтроки();
				Для каждого _ВыбранноеУпаковка Из ВыбранноеЗначение.Упаковка Цикл
					_СтрокаУпаковка = Объект.Упаковка.Добавить();
					_СтрокаУпаковка.КлючСтроки = ПолучитьКлючСтрокиУпаковка();
					_СтрокаУпаковка.КлючСвязи = _СтрокаПартии.КлючСтроки;
					_СтрокаУпаковка.Уровень = _ВыбранноеУпаковка.Уровень;
					_СтрокаУпаковка.Упаковка = _ВыбранноеУпаковка.Упаковка;
					_СтрокаУпаковка.Количество = _ВыбранноеУпаковка.Количество;
					Для каждого _ВыбранноеМаркировка Из _ВыбранноеУпаковка.Маркировка Цикл
						_СтрокаМаркировка = Объект.Маркировка.Добавить();
						_СтрокаМаркировка.КлючСвязи = _СтрокаУпаковка.КлючСтроки;
						_СтрокаМаркировка.Маркировка = _ВыбранноеМаркировка.Маркировка;
						_СтрокаМаркировка.КлассМаркировки = _ВыбранноеМаркировка.КлассМаркировки;
					КонецЦикла;
				КонецЦикла;
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаОтправить(Команда)
	
	Если Модифицированность ИЛИ НЕ Объект.Проведен Тогда
		Сообщить("Документ необходимо провести");
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("КомандаОтправитьОбработкаОповещения", ЭтаФорма), "Отправить транзакцию?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, "Отправка");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправитьОбработкаОповещения(Результат, Параметры) Экспорт
	
	Перем _Отказ, _Ошибка;
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	КомандаОтправитьНаСервере(Объект.Ссылка, _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
		Сообщить("Если партии отправлены, исходящие ВСД надо выбрать вручную");
	Иначе
		Прочитать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КомандаОтправитьНаСервере(пСсылка, пОтказ = Ложь, пОшибка = "")
	
	ВетисMercuryApplicationsСлой1с.TransportOperation(пСсылка, пОтказ, пОшибка);
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаАннулировать(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("КомандаАннулироватьОбработкаОповещения", ЭтаФорма), "Аннулировать транзакцию?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, "Аннулирование");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаАннулироватьОбработкаОповещения(Результат, Параметры) Экспорт
	
	Перем _Отказ, _Ошибка;
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	КомандаАннулироватьНаСервере(Объект.Ссылка, _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	Иначе
		Прочитать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КомандаАннулироватьНаСервере(пСсылка, пОтказ = Ложь, пОшибка = "")
	
	ВетисMercuryApplicationsСлой1с.WithdrawTransportOperation(пСсылка, "ошибка оформления", пОтказ, пОшибка);
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаПодборПартий(Команда)
	
	_Параметры = Новый Структура;
	
	_Параметры.Вставить("BusinessEntity", Объект.Отправитель);
	
	_Параметры.Вставить("Enterprise", Объект.ОтправительПредприятие);
	
	ОткрытьФорму("Документ.ВетисТранспортнаяПартия.Форма.ФормаПодбора", _Параметры, ЭтаФорма, ,,,Новый ОписаниеОповещения("ПодборПартийОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборПартийОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУсловияПеремещения(Команда)
	
	_Параметры = Новый Структура;
	
	_subProduct = Новый Массив;
	
	Для каждого _Строка Из Объект.Партии Цикл
		Если _subProduct.Найти(_Строка.ЗаписьСЖ) = Неопределено Тогда
			_subProduct.Добавить(_Строка.ЗаписьСЖ);
		КонецЕсли;
	КонецЦикла;
	
	_Параметры.Вставить("subProduct", ПолучитьСписок1(_subProduct));
	
	_Параметры.Вставить("shipmentRoute", Новый Массив);
	_Параметры.shipmentRoute.Добавить(Новый Структура("Предприятие", Объект.ОтправительПредприятие));
	_Параметры.shipmentRoute.Добавить(Новый Структура("Предприятие", Объект.ПолучательПредприятие));
	
	_Параметры.Вставить("BusinessEntity", Объект.Отправитель);
	
	ОткрытьФорму("Документ.ВетисТранспортнаяПартия.Форма.ФормаУсловияПеремещения", _Параметры, ЭтаФорма,,,,Новый ОписаниеОповещения("КомандаУсловияПеремещенияОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписок1(пСсылка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВетисStockEntry.SubProduct
	|ИЗ
	|	Справочник.ВетисStockEntry КАК ВетисStockEntry
	|ГДЕ
	|	ВетисStockEntry.Ссылка В(&Ссылка)");
	
	Запрос.УстановитьПараметр("Ссылка", пСсылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	_список = Новый Массив;
	Пока Выборка.Следующий() Цикл
		_список.Добавить(Выборка.SubProduct);
	КонецЦикла;
	
	Возврат _список;
	
КонецФункции

&НаКлиенте
Процедура КомандаУсловияПеремещенияОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		Объект.УсловияПеремещения.Очистить();
		Для каждого _Элемент Из пРезультат Цикл
			Для каждого _Условие Из _Элемент.Значение Цикл
				_УсловияПеремещенияСтрока = Объект.УсловияПеремещения.Добавить();
				_УсловияПеремещенияСтрока.subProduct = _Элемент.Ключ;
				_УсловияПеремещенияСтрока.Условие = _Условие;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьПоДокументу(Команда)
	
	КомандаЗаполнитьПоДокументуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура КомандаЗаполнитьПоДокументуНаСервере(пОтказ = Ложь, пОшибка = "")
	
	Если Объект.Подпитка И ЗначениеЗаполнено(Объект.ТранзакцияПодпитка) Тогда
		
		_Объект = Объект.ТранзакцияПодпитка.ПолучитьОбъект();
		
		//....
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтправительПриИзменении(Элемент)
	
	Если ОтправительСвязь Тогда
		Объект.ОтправительПредприятие = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ОтправительСвязь Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(Объект.Отправитель) Тогда
			ОткрытьФорму("Справочник.ВетисEnterprise.ФормаВыбора", Новый Структура("BusinessEntity", Объект.Отправитель),,,,,Новый ОписаниеОповещения("ОтправительПредприятиеОбработкаОповещения",ЭтаФорма));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительПредприятиеОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.ОтправительПредприятие = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительСвязьНажатие(Элемент)
	
	ОтправительСвязь = НЕ ОтправительСвязь;
	
	УстановитьВидимостьКлиент("ОтправительСвязь");
	
КонецПроцедуры


&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	Если ПолучательСвязь Тогда
		Объект.ПолучательПредприятие = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ПолучательСвязь Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(Объект.Получатель) Тогда
			ОткрытьФорму("Справочник.ВетисEnterprise.ФормаВыбора", Новый Структура("BusinessEntity", Объект.Получатель),,,,,Новый ОписаниеОповещения("ПолучательПредприятиеОбработкаОповещения",ЭтаФорма));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПредприятиеОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.ПолучательПредприятие = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательСвязьНажатие(Элемент)
	
	ПолучательСвязь = НЕ ПолучательСвязь;
	
	УстановитьВидимостьКлиент("ПолучательСвязь");
	
КонецПроцедуры


&НаКлиенте
Процедура ПартииПриАктивизацииСтроки(Элемент)
	
	_ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//Элементы.Упаковка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", _ТекущиеДанные.КлючСтроки);
	//
	//_СтрокиУпаковка = Объект.Упаковка.НайтиСтроки(Новый Структура("КлючСвязи", _ТекущиеДанные.КлючСтроки));
	//Если _СтрокиУпаковка.Количество() > 0 Тогда
	//	Элементы.Упаковка.ТекущаяСтрока = _СтрокиУпаковка[0];
	//	Элементы.Маркировка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", _СтрокиУпаковка[0].КлючСтроки);
	//Иначе
	//	Элементы.Маркировка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", 0);
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КлючСтроки = ПолучитьКлючСтроки();
		//Элементы.Упаковка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", Элемент.ТекущиеДанные.КлючСтроки);
		//Если НЕ Элементы.Упаковка.ТекущаяСтрока = Неопределено Тогда
		//	_ТекущиеДанные = Объект.Упаковка.НайтиПоИдентификатору(Элементы.Упаковка.ТекущаяСтрока);
		//	Если НЕ _ТекущиеДанные = Неопределено Тогда
		//		Элементы.Маркировка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", _ТекущиеДанные.КлючСтроки);
		//	КонецЕсли;
		//КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииПередУдалением(Элемент, Отказ)
	
	_ТекущиеДанные = Элементы.Партии.ТекущиеДанные;
	
	Для каждого _СтрокаУпаковка Из Объект.Упаковка.НайтиСтроки(Новый Структура("КлючСвязи", Элемент.ТекущиеДанные.КлючСтроки)) Цикл
		Для каждого _СтрокаМаркировка Из Объект.Маркировка.НайтиСтроки(Новый Структура("КлючСвязи", _СтрокаУпаковка.КлючСтроки)) Цикл
			Объект.Маркировка.Удалить(_СтрокаМаркировка);
		КонецЦикла;
		Объект.Упаковка.Удалить(_СтрокаУпаковка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииВСДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	_Статус = Новый СписокЗначений;
	_Статус.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Погашен"));
	_Статус.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Оформлен"));
	_Статус.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Аннулирован"));
	
	_Тип = Новый СписокЗначений;
	_Тип.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentType.Транспортный"));
	
	_Параметры = Новый Структура("Проведен, Статус, Тип, Входящие, Исходящие", Истина, _Статус, _Тип);
	
	_Параметры.Вставить("Отправитель", Объект.Отправитель);
	_Параметры.Вставить("Получатель", Объект.Получатель);
	
	_Параметры.Вставить("ОтправительПредприятие", Объект.ОтправительПредприятие);
	_Параметры.Вставить("ПолучательПредприятие", Объект.ПолучательПредприятие);
	
	_оо = Новый ОписаниеОповещения("ПартииВСДНачалоВыбораОбработкаОповещения", ЭтаФорма);
	
	ОткрытьФорму("Документ.ВетисВетеринарноСопроводительныйДокумент.ФормаВыбора", _Параметры,,,,, _оо);
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииВСДНачалоВыбораОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		Элементы.Партии.ТекущиеДанные.ВСД = Результат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииЗаписьСЖНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	_Параметры = Новый Структура;
	
	_Параметры.Вставить("BusinessEntity", Объект.Отправитель);
	
	_Параметры.Вставить("Enterprise", Объект.ОтправительПредприятие);
	
	_Параметры.Вставить("Ключ", Элементы.Партии.ТекущиеДанные.ЗаписьСЖ);
	
	ОткрытьФорму("Справочник.ВетисStockEntry.ФормаВыбора", _Параметры, ЭтаФорма, ,,,Новый ОписаниеОповещения("ПартииЗаписьСЖНачалоВыбораОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииЗаписьСЖНачалоВыбораОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		Элементы.Партии.ТекущиеДанные.ЗаписьСЖ = Результат;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура УпаковкаПриАктивизацииСтроки(Элемент)
	
	_ТекущиеДанные = Элементы.Упаковка.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Маркировка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", _ТекущиеДанные.КлючСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Элементы.Партии.ТекущиеДанные = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КлючСвязи = Элементы.Партии.ТекущиеДанные.КлючСтроки;
		Элемент.ТекущиеДанные.КлючСтроки = ПолучитьКлючСтрокиУпаковка();
		Элементы.Маркировка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", Элемент.ТекущиеДанные.КлючСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПередУдалением(Элемент, Отказ)
	
	Для каждого _Строка Из Объект.Маркировка.НайтиСтроки(Новый Структура("КлючСвязи", Элемент.ТекущиеДанные.КлючСтроки)) Цикл
		Объект.Маркировка.Удалить(_Строка);
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура МаркировкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Элементы.Упаковка.ТекущиеДанные = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркировкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	_ТекущиеДанные = Элементы.Упаковка.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КлючСвязи = _ТекущиеДанные.КлючСтроки;
		Элемент.ТекущиеДанные.КлассМаркировки = ПредопределенноеЗначение("Перечисление.ВетисProductMarkingClass.UNDEFINED");
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВСДПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры


&НаКлиенте
Процедура ТТНПриИзменении(Элемент)
	
	Если Элемент = Элементы.ттнТранспорт Тогда
		ттнТранспортПриИзменении(Элемент);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ттнТранспортПриИзменении(Элемент)
	
	ттнТранспортПриИзмененииНаСервере();
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	Для каждого _Реквизит Из ПолучитьРеквизиты() Цикл
		
		Для каждого _Строка Из Объект.ВСД Цикл
			Если Лев(_Реквизит.Имя, 3) = "всд" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 4)];
			КонецЕсли;
		КонецЦикла;
		
		Для каждого _Строка Из Объект.ТТН Цикл
			Если Лев(_Реквизит.Имя, 3) = "ттн" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 4)];
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьРеквизитыФормы()
	
	Объект.ВСД.Очистить();
	Объект.ТТН.Очистить();
	
	_СтрокаВСД = Объект.ВСД.Добавить();
	_СтрокаТТН = Объект.ТТН.Добавить();
	
	Для каждого _Реквизит Из ПолучитьРеквизиты() Цикл
		Если Лев(_Реквизит.Имя, 3) = "всд" Тогда
			_СтрокаВСД[Сред(_Реквизит.Имя, 4)] = ЭтаФорма[_Реквизит.Имя];
		ИначеЕсли Лев(_Реквизит.Имя, 3) = "ттн" Тогда
			_СтрокаТТН[Сред(_Реквизит.Имя, 4)] = ЭтаФорма[_Реквизит.Имя];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьКлючСтроки()
	
	_список = Объект.Партии.Выгрузить().ВыгрузитьКолонку("КлючСтроки");
	
	Для _Индекс = 1 По 999 Цикл
		Если _список.Найти(_Индекс) = Неопределено Тогда
			Возврат _Индекс;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ПолучитьКлючСтрокиУпаковка()
	
	_список = Объект.Упаковка.Выгрузить().ВыгрузитьКолонку("КлючСтроки");
	
	Для _Индекс = 1 По 999 Цикл
		Если _список.Найти(_Индекс) = Неопределено Тогда
			Возврат _Индекс;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции


&НаКлиенте
Процедура УстановитьВидимостьКлиент(пИмяСобытия = Неопределено)
	
	Если пИмяСобытия = "ОтправительСвязь" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.ОтправительСвязь.Подсказка = ?(ОтправительСвязь, "с учетом связи", "без учета связи");
		Элементы.ОтправительСвязь.Картинка = ?(ОтправительСвязь, БиблиотекаКартинок.ВетисЕстьСвязь, БиблиотекаКартинок.ВетисНетСвязи);
	КонецЕсли;
	
	Если пИмяСобытия = "ПолучательСвязь" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.ПолучательСвязь.Подсказка = ?(ПолучательСвязь, "с учетом связи", "без учета связи");
		Элементы.ПолучательСвязь.Картинка = ?(ПолучательСвязь, БиблиотекаКартинок.ВетисЕстьСвязь, БиблиотекаКартинок.ВетисНетСвязи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость(пИмяСобытия = Неопределено)
	
	Если пИмяСобытия = Неопределено ИЛИ пИмяСобытия = "ВетисНастройки" ИЛИ пИмяСобытия = "ВетисВерсия" Тогда
		
		_Версия20 = Ветис.Версия_2_0();
		
		Элементы.всдВетсанэкспертиза.Видимость = НЕ _Версия20;
		Элементы.всдВетсанэкспертиза20.Видимость = _Версия20;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьНаСервере()
	
	_Объект = РеквизитФормыВЗначение("Объект");
	
	_Объект.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(_Объект, "Объект");
	
КонецПроцедуры

&НаСервере
Функция ттнТранспортПриИзмененииНаСервере()
	
	ттнНомерТранспорта = ттнТранспорт.Номер;
	
	ттнНомерПрицепа = ттнТранспорт.НомерПрицепа;
	
	ттнТипТранспорта = ттнТранспорт.ТипТранспорта;
	
	ттнПосредник = ттнТранспорт.Перевозчик;
	
	ттнСпособХранения = ттнТранспорт.СпособХранения;
	
КонецФункции
