
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	shipmentRouteList.ЗагрузитьЗначения(Параметры.shipmentRoute);
	
	StockEntryList.ЗагрузитьЗначения(Параметры.StockEntryList);
	
	BusinessEntity = Параметры.BusinessEntity;
	
	КомандаЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВсеДочерние(Список, Истина);
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаГотово(Команда)
	
	ЗначениеВыбора = Новый Соответствие;
	
	Для каждого _Уровень1 Из Список.ПолучитьЭлементы() Цикл
		Для каждого _Уровень2 Из _Уровень1.ПолучитьЭлементы() Цикл
			Для каждого _Уровень3 Из _Уровень2.ПолучитьЭлементы() Цикл
				Для каждого _Уровень4 Из _Уровень3.ПолучитьЭлементы() Цикл
					Если _Уровень4.Использование Тогда
						Для каждого _SubProduct Из _Уровень2.Значение Цикл
							_Список = ЗначениеВыбора.Получить(_SubProduct.Значение);
							Если _Список = Неопределено Тогда
								_Список = Новый Массив;
								ЗначениеВыбора.Вставить(_SubProduct.Значение, _Список);
							КонецЕсли;
							Для каждого _Условие Из _Уровень4.Значение Цикл
								Если _Список.Найти(_Условие.Значение) = Неопределено Тогда
									_Список.Добавить(_Условие.Значение);
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Закрыть(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	Перем _Отказ, _Ошибка;
	
	КомандаЗаполнитьНаСервере(_Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	Иначе
		ВсеДочерние(Список, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КомандаЗаполнитьНаСервере(пОтказ = Ложь, пОшибка = "")
	
	Список.ПолучитьЭлементы().Очистить();
	
	_Ответ = ВетисMercuryApplicationsСлой1с.CheckShipmentRegionalization(StockEntryList.ВыгрузитьЗначения(), shipmentRouteList.ВыгрузитьЗначения(), BusinessEntity, пОтказ, пОшибка);
	
	Если пОтказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	_Уровень1 = Список.ПолучитьЭлементы().Добавить();
	_Уровень1.Уровень = 1;
	_Уровень1.Имя = "Корень";
	_Уровень1.ПредставлениеЗначения = "Все условия";
	_Уровень1.ЭтоГруппа = Истина;
	
	Для каждого _r13nRouteSection Из _Ответ Цикл
		Для каждого _appliedR13nRule Из _r13nRouteSection.appliedR13nRule Цикл
			Если _appliedR13nRule.decision = ВетисКонстанты.RegionalizationDecision_РазрешеноУсловно() Тогда
				_Уровень2 = _Уровень1.ПолучитьЭлементы().Добавить();
				_Уровень2.Уровень = 2;
				_Уровень2.Имя = "Вид продукции";
				_Уровень2.Значение = Новый СписокЗначений;
				_Уровень2.ПредставлениеЗначения = "";
				_Уровень2.ЭтоГруппа = Истина;
				Для каждого _cargoType Из _appliedR13nRule.cargoType Цикл
					_cargoType = ВетисОбменДанными.ОбработатьСсылку(_cargoType);
					//_cargoType = ВетисDictionaryСлой1с.SubProduct(_cargoType);
					_Уровень2.Значение.Добавить(_cargoType);
					_Уровень2.ПредставлениеЗначения = _Уровень2.ПредставлениеЗначения + ?(_Уровень2.ПредставлениеЗначения="","", "; ") + _cargoType;// + " (" + СокрП(_cargoType.code) + ")";
				КонецЦикла;
				Для каждого _requirement Из _appliedR13nRule.requirement Цикл
					_Уровень3 = _Уровень2.ПолучитьЭлементы().Добавить();
					_Уровень3.Уровень = 3;
					_Уровень3.Имя = "Группы";
					_Уровень3.ПредставлениеЗначения = _requirement.relatedDisease.name;
					_Уровень3.ЭтоГруппа = Истина;
					Для каждого _conditionGroup Из _requirement.conditionGroup Цикл
						_Уровень4 = _Уровень3.ПолучитьЭлементы().Добавить();
						_Уровень4.Уровень = 4;
						_Уровень4.Имя = "Условия";
						_Уровень4.Значение = Новый СписокЗначений;
						_Уровень4.ПредставлениеЗначения = "";
						_Номер = 1;
						Для каждого _condition Из _conditionGroup.condition Цикл
							_Уровень4.Значение.Добавить(ВетисОбменДанными.ОбработатьСсылку(_condition));
							//_Уровень4.Значение.Добавить(ВетисDictionaryСлой1с.RegionalizationCondition(_condition));
							_Уровень4.ПредставлениеЗначения = _Уровень4.ПредставлениеЗначения + ?(_Уровень4.ПредставлениеЗначения="","", "; ") + _Номер + ") " + _condition.text;
							_Номер = _Номер + 1;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура СписокИспользованиеПриИзменении(Элемент)
	
	_ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ВсеРодительские(_ТекущиеДанные, _ТекущиеДанные.Использование);
	
	ВсеДочерние(_ТекущиеДанные, _ТекущиеДанные.Использование);
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеРодительские(пТекущиеДанные, пЗначение)
	
	_ТекущийРодитель = пТекущиеДанные.ПолучитьРодителя();
	Если НЕ _ТекущийРодитель = Неопределено Тогда
		
		Если пЗначение = Ложь Тогда
			_ЕстьИстина = Ложь;
			Для каждого _Элемент Из _ТекущийРодитель.ПолучитьЭлементы() Цикл
				Если _Элемент.Использование Тогда
					_ЕстьИстина = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если _ЕстьИстина = Истина Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		_ТекущийРодитель.Использование = пЗначение;
		
		ВсеРодительские(_ТекущийРодитель, пЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеДочерние(пТекущиеДанные, пЗначение)
	
	_ЭтоПервый = Истина;
	Для каждого _Элемент Из пТекущиеДанные.ПолучитьЭлементы() Цикл
		_Элемент.Использование = (НЕ _Элемент.Уровень = 4 ИЛИ _ЭтоПервый) И пЗначение;
		ВсеДочерние(_Элемент, пЗначение);
		_ЭтоПервый = Ложь;
	КонецЦикла;
	
КонецПроцедуры

//http://vetrf.ru/vetrf-forum/posts/list/645/8354.page#68610
//там написано, что сертифицирующий офишиал (это все наши три группы) производят сертификацию на основе чувства обоснованной уверенности
//в безопасности и соответствии сертифицируемого, основывающейся на том, что он лично знает или на данных из официальных источников.
//Таким образом, если вы оптовики, то вы имеете входящие сертификаты, т.е. документы с этими самыми официальными данными.
//К ним вы добавляете свое знание о товаре (хранился правильно, не загрязнен, не контаминирован), происходит от животных из такой-то местности
//(эти данные есть в цепочке сертификатов), вы, если сомневаетесь, проверяете статус своего региона и региона назначения в нашем сервисе.
//Если вы собрали достаточно данных об исполнении условий и имеете чувство обоснованной уверенности - сертифицируете.
//Если не имеете такого чувства, то не сертифицируете.
