
&НаКлиенте
Процедура КомандаУстановить(Команда)
	
	ПоказатьВводДаты(Новый ОписаниеОповещения("ОбработкаОповещения", ЭтаФорма), ДатаСеанса());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		_список = Новый Массив;
		Для каждого _Строка Из Элементы.Список.ВыделенныеСтроки Цикл
			_список.Добавить(Новый Структура("Операция,BusinessEntity,Enterprise,Граница",_Строка.Операция,_Строка.BusinessEntity,_Строка.Enterprise, Результат));
		КонецЦикла;
		УстановитьНаСервере(_список);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДатаСеанса()
	Возврат ТекущаяДатаСеанса();
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьНаСервере(пСписок)
	
	Для каждого _Строка Из пСписок Цикл
		_мз = РегистрыСведений.ВетисГраницыЗапросов.СоздатьМенеджерЗаписи();
		_мз.Операция = _Строка.Операция;
		_мз.BusinessEntity = _Строка.BusinessEntity;
		_мз.Enterprise = _Строка.Enterprise;
		_мз.Граница = _Строка.Граница;
		_мз.Записать();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастроитьСвязь(Команда)
	
	_ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.ВетисBusinessMembers.ФормаЗаписи", ПолучитьПараметры(_ТекущиеДанные.BusinessEntity, _ТекущиеДанные.Enterprise));
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметры(pBusinessEntity, pEnterprise)
	
	_Запись = РегистрыСведений.ВетисBusinessMembers.СоздатьМенеджерЗаписи();
	_Запись.BusinessEntity = pBusinessEntity;
	_Запись.Enterprise = pEnterprise;
	_Запись.Прочитать();
	
	Если _Запись.Выбран() Тогда
		Возврат Новый Структура("Ключ", РегистрыСведений.ВетисBusinessMembers.СоздатьКлючЗаписи(Новый Структура("BusinessEntity, Enterprise", pBusinessEntity, pEnterprise)));
	Иначе
		Возврат Новый Структура("BusinessEntity, Enterprise", pBusinessEntity, pEnterprise);
	КонецЕсли;
	
КонецФункции
